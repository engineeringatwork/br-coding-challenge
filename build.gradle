buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
		metaModelsSourcesDir = project.file("metamodels")
		querydslVersion = "4.1.4"
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.bottlerocket'
version = '1.0'
sourceCompatibility = 1.8

bootJar  {
    baseName = 'rocket-coding-challenge'
    version = '1.0'
}

sourceSets {
	main {
		java {
			srcDir metaModelsSourcesDir
		}
	}
}

repositories {
	mavenCentral()
}

configurations {
	querydslapt
}

task querymodels(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query meta models for domain models') {
		doFirst {
			delete metaModelsSourcesDir;
			metaModelsSourcesDir.mkdirs();
		}
			
		classpath = configurations.compile + configurations.querydslapt
		destinationDir = metaModelsSourcesDir
		
		source = sourceSets.main.java
		options.compilerArgs = [
				"-proc:only",
				"-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor",
				"-s", metaModelsSourcesDir
		]
}

dependencies 
{
   querydslapt("com.querydsl:querydsl-apt:${querydslVersion}")
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('com.h2database:h2')
	
	 // Swagger Dependencies for API Documentation
	compile("io.springfox:springfox-swagger2:2.7.0")
	compile("io.springfox:springfox-swagger-ui:2.7.0")
	
	// QueryDSL Dependencies for Database Change Management	
	compile("com.querydsl:querydsl-jpa:${querydslVersion}")
	compile("com.querydsl:querydsl-core:${querydslVersion}")
	
	testCompile('org.springframework.boot:spring-boot-starter-test')
}
